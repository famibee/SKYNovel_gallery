[title text='top page'][call label=*init]
**
	[fg layer=0 fn=face_柊24_制服 face=face_柊24_表情_口_笑顔,face_柊24_表情_目_瞬き通常,face_柊24_表情_マユゲ_通常,face_柊24_前髪 time=0]

　革新的ノベルゲームエンジン登場──。WebGLによる高速描画。[r]
　𩸽《ほっけ》なルビや[tcy t="451" r=縦中横]なども。[r]
　文字修飾は[span style='color: tomato;']CSS Style[span]で指定。[r]
　[span style='font-family: my_himajihoso; color: skyblue;']フォント[span]も自由に変更。[r]
[r][r][r][r][r][r][r]
　スプライトシートも簡単に作れ、[r]インライン表示[graph pic=sankaku]する事も。[r]
　[link label=*目を輝かせる global=true call=true]文字リンク[endlink]を押すと柊《ひいらぎ》さんが[r]目を輝かせます。[r]
　クリックでアニメを止めないまま[ch text='[trans]']します。
[l][clear_text]

	[lay layer=bg page=back fn=013ELLY18412_TP_V4]
;	[lay layer=0 page=back fn=tor_斬撃_曲線 left=100 top=120]
	[lay layer=mes page=back b_alpha=0 filter="DropShadowFilter"]
	[trans layer='bg,mes' time=2000 rule='r_uzumaki']
	[wt]


;*********************
　各種イベント検知。キーボードの「Shift」「P」を押してみて下さい。[r]
　「P」でスナップショット（キーは変えられます）。[r]
　マウスのホイールダウン（アップは読み進め）も。[r]
[r][r][r][r][r][r][r]
　外部DOM要素のクリックも検知出来ます。（このページ上部のスナップショットボタンで使用してます）[r]
[r]
　また、素材画像なしで手軽にボタンが作れます。
[button text=押せます left=40 top=380 page=fore label=*title arg='push button' global=true call=true]


[l][clear_text]
;*********************
	[lay layer=bg page=back fn=kuma425008_TP_V4]
	[trans layer='bg,0,mes' time=2000]
	[wt]

[jump label=**before]


;*********************
*目を輝かせる
	[fg fn=face_柊24_制服 face=face_柊24_表情_口_笑顔・中,face_柊24_表情_目_瞬きびっくり,face_柊24_表情_マユゲ_通常,face_柊24_前髪]
	[return]


;*********************
*init
	[add_lay layer=bg class=grp]
	[add_lay layer=0 class=grp]
	[add_lay layer=mes class=txt]

	[lay layer=bg page=back fn=002kumakichi0327_TP_V4 face=bg_mask]
	[lay layer=mes page=back left=8 top=6 style="width: 718px; height: 480px; writing-mode: vertical-rl; padding-top: 12px; padding-bottom: 8px; padding-left: 10px; padding-right: 22px;" filter="DropShadowFilter"]
	; color: white; b_alpha=0.5 b_color=0x880000
	[trans layer='bg,mes' time=0][wt]
[event key=F7 label=*stats global=true call=true]
[event key=p label=*snapshot global=true call=true]
;[event key=w label=*toggle_full_screen global=true call=true]

[event key=rightclick label=*title arg='rightclick' global=true call=true]
[event key=a label=*title arg='a Key' global=true call=true]
[event key=Shift label=*title arg='Shift Key' global=true call=true]
[event key=wheel.y<0 label=*title arg='wheel down' global=true call=true]

[event key=gamepadconnected label=*title arg='USB gamepad connected' global=true call=true]

;[event key=swipeleft label=*gesture arg='swipeleft' global=true call=true]
;[event key=swiperight label=*gesture arg='swiperight' global=true call=true]
;[event key=swipeup label=*gesture arg='swipeup' global=true call=true]
;[event key=swipedown label=*gesture arg='swipedown' global=true call=true]
;[event key=press label=*gesture arg='press' global=true call=true]
;[event key=tap label=*gesture arg='tap' global=true call=true]
;[event key=tap2 label=*gesture arg='tap2' global=true call=true]

	[call fn=face_*]
	[call fn=ext_*]

	[event key='dom=#snapshot' label=*snapshot global=true call=true need_err=false]
	[dump_script set_fnc=set_ed break_fnc=break_ed need_err=false]
	[toggle_full_screen key=w]	; for browser
[return]


;*********************
*gesture
[title text="&sn.eventArg +' pointers:'+ sn.eventArg.pointers +' !'"][return]



*stats
[event key=F7 global=true del=true][stats][return]

*snapshot
[snapshot fn=pic][return]

*toggle_full_screen
[toggle_full_screen][return]

*title
[title text="&sn.eventArg +' !'"][return]
