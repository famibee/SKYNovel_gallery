[title text='文字ボタン・リンク'][call label=*init]

[add_lay layer=mes class=txt]

[macro name=rs][r][s][endmacro]
[char2macro char=/ name=rs]

[macro name=plc][p][er][endmacro]
[char2macro char=¥ name=plc]

*main
　Ｑ１．【戎橋】の読み方を選択して下さい。[r]
[r]
　[link label=*yoko style='background-color: rgba(0,180,180,0.5);' style_hover='background-color: rgba(200,200,200,0.9);' hint='行方向の変更']横書き[endlink]
　[link label=*tate style='background-color: rgba(180,180,0,0.5);' style_hover='background-color: rgba(200,200,200,0.9);' hint='行方向の変更']縦書き[endlink]

[button text=かいばし left=120 top=230 page=fore rotation=50 label=**after arg=かいばし style='{"fill": "deepskyblue"}' b_pic=ele_mini1 hint='選択肢']
[button text=えびすばし left=260 top=260 page=fore rotation=-70 label=***after arg=えびすばし style='{"fill": "mediumslateblue"}' b_pic=ele_mini1 hint='選択肢']
;	[button text=えびすばし left=260 top=260 page=fore rotation=-70 label=***after arg=えびすばし style='{"fill": "mediumslateblue"}' b_pic=ele_mini1 hint='選択肢' hint_tate=false]
[button text=いかいのばし left=460 top=210 page=fore rotation=-25 label=**after arg=いかいのばし style='{"fill": "tomato"}' b_pic=ele_mini2 hint='選択肢']

[button text=既読スキップ left=550 top=300 page=fore label=*go_skip call=true style='{"fill": "aqua"}' style_hover='{"fill": "#4444ff"}' hint='背景なし文字ボタン']

[button pic=MnuUp_btnPage2 left=560 top=350 page=fore rotation=25 label=****after hint='これだけ画像ボタン']
;	[button pic=MnuUp_btnPage2 left=340 top=250 page=fore rotation=25 label=****after hint='これだけ画像ボタン' scale_x=0.7 scale_y=5]
;	[button pic=MnuUp_btnPage2 left=380 top=350 page=fore rotation=25 label=****after hint='これだけ画像ボタン' width=150]
;	[button pic=MnuUp_btnPage2 left=560 top=350 page=fore rotation=25 label=****after hint='これだけ画像ボタン' height=135]

[button text=横書き left=20 top=430 page=fore label=*yoko style='{"fill": "bisque"}' style_hover='{"fill": "#4444ff"}' hint='行方向の変更']
[button text=縦書き left=20 top=460 page=fore label=*tate style='{"fill": "bisque"}' style_hover='{"fill": "#4444ff"}' hint='行方向の変更']
[s]
**
[er][r]
？「ええいお話にならぬわぁぁぁ！！　道頓堀の水で顔を洗ってで直してくるがいい！！」¥
[jump label=*main]

***
[er][r]
[button text=えびすばし left=260 top=260 page=fore rotation=-70 label=***after arg=えびすばし style='{"fill": "mediumslateblue"}' b_pic=ele_mini1 enabled=false]
　正解！¥

****
;[er]
　Ｑ２．【日本橋】の読み方は？[r]
　　[link label=**after wait=1000]にほんばし[endlink][r]
　　[link label=***after style='background-color: rgba(200,255,200,0.5);']にっぽんばし[endlink]
;　　[link label=**after wait=1000 hint_tate=false hint='背景なし文字ボタン']にほんばし[endlink][r]
;　　[link label=***after style='background-color: rgba(200,255,200,0.5);' hint='背景なし文字ボタン']にっぽんばし[endlink]
[s]
**
[er][r]
？「貴様関東人だな！　初めからやり直すが良い！」¥
[jump label=*main]

***
[er][r]
　正解！¥

;　Ｑ３．？[r]
　以上です。[p][er]

	; サンプルは最初に戻るが、既読スキップすると永久ループになるので、その対策
	&sn.tagL.enabled = true		; 頁末まで一気に読み進むか(l無視)
	&sn.skip.enabled = false	; 次の選択肢(/未読)まで進むが有効か
	&sn.auto.enabled = false	; 自動読みすすみモードかどうか
[jump label=*main]


*yoko
	[lay layer=mes style='writing-mode: horizontal-tb;']
	[clear_lay][jump label=*main]
*tate
	[lay layer=mes style='writing-mode: vertical-rl;']
	[clear_lay][jump label=*main]

*go_skip
	&sn.skip.enabled = true
	[set_cancel_skip]
[return]

*init
	[event key='dom=#snapshot' label=*snapshot global=true call=true need_err=false]
	[dump_script set_fnc=set_ed break_fnc=break_ed need_err=false]
	[toggle_full_screen key=w]	; for browser

	[event global=true call=true key=ArrowLeft label=*set_focus arg=prev]
	[event global=true call=true key=ArrowRight label=*set_focus arg=next]
[return]

*set_focus	[set_focus to=&sn.eventArg][return]
*snapshot	[snapshot fn=pic][return]
